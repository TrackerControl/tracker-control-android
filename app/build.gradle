apply plugin: 'com.android.application'

android {
    compileSdkVersion = 30

    defaultConfig {
        applicationId = "net.kollnig.missioncontrol"
        minSdkVersion 22
        targetSdkVersion 30
        versionCode 2020071101
        versionName "2020.07.11"
        archivesBaseName = "TrackerControl-$versionName" // name of apk

        externalNativeBuild {
            cmake {
                cppFlags ""
                arguments "-DANDROID_PLATFORM=android-22"
                // https://developer.android.com/ndk/guides/cmake.html
            }
        }

        // ndkVersion "21.0.6113669"
        ndk {
            // https://developer.android.com/ndk/guides/abis.html#sa
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
    }

    lintOptions {
        disable 'MissingTranslation'
        quiet false
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    buildTypes {
        release {
            //minifyEnabled = true
            //proguardFiles.add(file('proguard-rules.pro'))
        }
        debug {
            //minifyEnabled = true
            //proguardFiles.add(file('proguard-rules.pro'))
        }
        flavorDimensions "version"
        productFlavors {
            play {
                applicationIdSuffix ".play"
                versionNameSuffix "-play"
                buildConfigField "String", "HOSTS_FILE_URI", "\"\""
                buildConfigField "String", "GITHUB_LATEST_API", "\"\""
            }
            fdroid {
                applicationIdSuffix ".fdroid"
                versionNameSuffix "-fdroid"
                buildConfigField "String", "HOSTS_FILE_URI", "\"https://www.netguard.me/hosts\""
                buildConfigField "String", "GITHUB_LATEST_API", "\"\""
            }
            github {
                buildConfigField "String", "HOSTS_FILE_URI", "\"https://www.netguard.me/hosts\""
                buildConfigField "String", "GITHUB_LATEST_API", "\"https://api.github.com/repos/OxfordHCC/tracker-control-android/releases\""
            }
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    packagingOptions { // due to geoip2
        exclude 'META-INF/DEPENDENCIES'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // https://developer.android.com/jetpack/androidx/releases/
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.1.0'
    annotationProcessor 'androidx.annotation:annotation:1.1.0'

    // https://bumptech.github.io/glide/
    implementation('com.github.bumptech.glide:glide:4.10.0') {
        exclude group: "com.android.support"
    }
    annotationProcessor 'com.github.bumptech.glide:compiler:4.10.0'

    implementation 'com.opencsv:opencsv:4.6'
    implementation 'com.maxmind.geoip2:geoip2:2.14.0'

    // Crash reporting
    implementation "ch.acra:acra-mail:5.6.2"
    implementation "ch.acra:acra-dialog:5.6.2"
    compileOnly "com.google.auto.service:auto-service-annotations:1.0-rc6"
    annotationProcessor "com.google.auto.service:auto-service:1.0-rc6"
}

// Fix lint error on release builds
configurations {
    all {
        exclude module: 'commons-logging'
        exclude module: 'httpclient' // due to geoip2
    }
}
