apply plugin: 'com.android.application'

android {
    compileSdk 35

    defaultConfig {
        applicationId "net.kollnig.missioncontrol"
        minSdk 23
        targetSdk 35
        //noinspection HighAppVersionCode
        versionCode 2025121801
        versionName "2025.12.18"

        externalNativeBuild {
            cmake {
                cppFlags ""
                arguments "-DANDROID_PLATFORM=android-22"
                // https://developer.android.com/ndk/guides/cmake.html
            }
        }

        //ndkVersion "27.2.12479018"
        ndk {
            // https://developer.android.com/ndk/guides/abis.html#sa
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    dependenciesInfo {
        // Disables dependency metadata when building APKs.
        includeInApk = false
        // Disables dependency metadata when building Android App Bundles.
        includeInBundle = false
    }

    buildTypes {
        release {
            minifyEnabled = true
            proguardFiles.add(file('proguard-rules.pro'))
        }
        debug {
            //minifyEnabled = true
            //proguardFiles.add(file('proguard-rules.pro'))
        }
    }

    flavorDimensions = ["version"]
    productFlavors {
        play {
            applicationIdSuffix ".play"
            versionNameSuffix "-play"
            buildConfigField "String", "HOSTS_FILE_URI", "\"\""
            buildConfigField "String", "GITHUB_LATEST_API", "\"\""

            externalNativeBuild.cmake {
                cFlags "-DPLAY"
            }
        }
        fdroid {
            applicationIdSuffix ".fdroid"
            versionNameSuffix "-fdroid"
            buildConfigField "String", "HOSTS_FILE_URI", "\"https://raw.githubusercontent.com/StevenBlack/hosts/master/hosts\""
            buildConfigField "String", "GITHUB_LATEST_API", "\"\""
        }
        github {
            buildConfigField "String", "HOSTS_FILE_URI", "\"https://raw.githubusercontent.com/StevenBlack/hosts/master/hosts\""
            buildConfigField "String", "GITHUB_LATEST_API", "\"https://api.github.com/repos/TrackerControl/tracker-control-android/releases\""

        }
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            outputFileName = "TrackerControl-${variant.name}-latest.apk"
        }
    }

    signingConfigs {
        release {
            enableV1Signing = true
            enableV2Signing = true
        }
    }

    compileOptions {
        encoding = "UTF-8"
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
        coreLibraryDesugaringEnabled = true
    }

    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES']
        }
    }

    lint {
        disable 'MissingTranslation'
    }
    namespace = 'net.kollnig.missioncontrol'
    buildFeatures {
        buildConfig = true
    }

    androidResources {
        generateLocaleConfig = true
    }
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // https://developer.android.com/jetpack/androidx/releases/
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'androidx.work:work-runtime:2.11.0'
    implementation 'com.google.guava:guava:33.5.0-android'
    annotationProcessor 'androidx.annotation:annotation:1.9.1'

    // fix errors with libraries
    def lifecycle_version = '2.10.0'
    //implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"

    // https://bumptech.github.io/glide/
    def glide_version = "5.0.5"
    implementation("com.github.bumptech.glide:glide:$glide_version") {
        exclude group: "com.android.support"
    }
    annotationProcessor "com.github.bumptech.glide:compiler:$glide_version"

    implementation 'com.opencsv:opencsv:5.12.0'
    implementation 'com.maxmind.geoip2:geoip2:4.3.1'

    // Crash reporting
    implementation "ch.acra:acra-mail:5.13.1"
    implementation "ch.acra:acra-dialog:5.13.1"
    compileOnly "com.google.auto.service:auto-service-annotations:1.1.1"
    annotationProcessor "com.google.auto.service:auto-service:1.1.1"

    // Tracker detection
    implementation 'org.smali:dexlib2:2.5.2'

    implementation 'com.caverock:androidsvg-aar:1.4'
}

// Fix lint error on release builds
configurations {
    configureEach {
        exclude module: 'commons-logging'
        exclude module: 'httpclient' // due to geoip2
    }
}
