apply plugin: 'com.android.application'

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "net.kollnig.missioncontrol"
        minSdkVersion 22
        targetSdkVersion 30
        //noinspection HighAppVersionCode
        versionCode 2021121001
        versionName "2021.12.10"

        externalNativeBuild {
            cmake {
                cppFlags ""
                arguments "-DANDROID_PLATFORM=android-22"
                // https://developer.android.com/ndk/guides/cmake.html
            }
        }

        ndkVersion "21.4.7075529"
        ndk {
            // https://developer.android.com/ndk/guides/abis.html#sa
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
    }

    lintOptions {
        disable 'MissingTranslation'
        quiet false
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    buildTypes {
        release {
            //minifyEnabled = true
            //proguardFiles.add(file('proguard-rules.pro'))
        }
        debug {
            //minifyEnabled = true
            //proguardFiles.add(file('proguard-rules.pro'))
        }
    }

    flavorDimensions "version"
    productFlavors {
        play {
            applicationIdSuffix ".play"
            versionNameSuffix "-play"
            buildConfigField "String", "HOSTS_FILE_URI", "\"\""
            buildConfigField "String", "GITHUB_LATEST_API", "\"\""

            externalNativeBuild.cmake {
                cFlags "-DPLAY"
            }
        }
        fdroid {
            applicationIdSuffix ".fdroid"
            versionNameSuffix "-fdroid"
            buildConfigField "String", "HOSTS_FILE_URI", "\"https://raw.githubusercontent.com/StevenBlack/hosts/master/hosts\""
            buildConfigField "String", "GITHUB_LATEST_API", "\"\""
        }
        github {
            buildConfigField "String", "HOSTS_FILE_URI", "\"https://raw.githubusercontent.com/StevenBlack/hosts/master/hosts\""
            buildConfigField "String", "GITHUB_LATEST_API", "\"https://api.github.com/repos/OxfordHCC/tracker-control-android/releases\""

        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "TrackerControl-${variant.name}-latest.apk"
        }
    }

    signingConfigs {
        release {
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions { // due to geoip2
        exclude 'META-INF/DEPENDENCIES'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // https://developer.android.com/jetpack/androidx/releases/
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation 'com.google.android.material:material:1.4.0'
    annotationProcessor 'androidx.annotation:annotation:1.3.0'

    // https://bumptech.github.io/glide/
    def glide_version = "4.12.0"
    implementation("com.github.bumptech.glide:glide:$glide_version") {
        exclude group: "com.android.support"
    }
    annotationProcessor "com.github.bumptech.glide:compiler:$glide_version"

    implementation 'com.opencsv:opencsv:5.4'
    implementation 'com.maxmind.geoip2:geoip2:2.14.0'

    // Crash reporting
    implementation "ch.acra:acra-mail:5.7.0"
    implementation "ch.acra:acra-dialog:5.7.0"
    compileOnly "com.google.auto.service:auto-service-annotations:1.0-rc6"
    annotationProcessor "com.google.auto.service:auto-service:1.0-rc6"

    // Tracker detection
    implementation 'com.github.lanchon.dexpatcher:multidexlib2:2.3.4.r2'

    implementation 'com.caverock:androidsvg-aar:1.4'
}

// Fix lint error on release builds
configurations {
    all {
        exclude module: 'commons-logging'
        exclude module: 'httpclient' // due to geoip2
    }
}
